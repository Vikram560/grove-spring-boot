buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath "org.springframework.boot:spring-boot-gradle-plugin:2.1.1.RELEASE"
	}
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

repositories {
	jcenter()
	mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
	compile "org.springframework.boot:spring-boot-starter-web"
	compile "com.marklogic:marklogic-client-api:4.1.1"

	testCompile "org.springframework.boot:spring-boot-starter-test"
	testCompile "com.marklogic:marklogic-junit:0.11.0"
	testCompile "org.xmlunit:xmlunit-core:2.6.2"
	testCompile "org.xmlunit:xmlunit-assertj:2.6.2"

	// Neither Gradle nor Intellij are happy without both of these included, not sure why
	testRuntime "org.junit.jupiter:junit-jupiter-api:5.3.0"
	testRuntime "org.junit.jupiter:junit-jupiter-engine:5.3.0"
}

// Needed by Gradle 4.6+ - see https://www.petrikainulainen.net/programming/testing/junit-5-tutorial-running-unit-tests-with-gradle/
test {
	useJUnitPlatform()
}

task buildPackage (type: Zip) {
	from("src/main") {
		into("spring-boot/src/main")
	}
	from(".") {
		include "app.build.gradle"
		into("spring-boot")
		rename {
			"build.gradle"
		}
	}
	from(".") {
		include "app.gradle.properties"
		into("spring-boot")
		rename {
			"gradle.properties"
		}
	}
	from(".") {
		include "app.README.md"
		into("spring-boot")
		rename {
			"README.md"
		}
	}
	baseName "grove-spring-boot"
}